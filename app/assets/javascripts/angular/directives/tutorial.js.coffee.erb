DunnoApp = angular.module('DunnoApp')

tutorialCtrl = ($scope, $rootScope)->

  $rootScope.$on('$locationChangeStart', -> $scope.$emit("modal.dismiss"))

  $scope.currentStep = 1
  $scope.steps = []

  hide = ->
    $scope.$emit("modal.dismiss")
    $scope.onClose()

  $scope.nextStep = ->
    if $scope.currentStep + 1 > $scope.steps.length
      hide()
    else
      $scope.currentStep++

  $scope.setCurrentStep = (step)->
    $scope.currentStep = step

  @increaseStep = ->
    $scope.steps.push($scope.steps.length + 1)

DunnoApp.controller 'tutorialCtrl', tutorialCtrl
DunnoApp.directive 'tutorial', ($parse, AssetsPreloader) ->
  restrict: 'E'
  transclude: true
  replace: true
  templateUrl: '<%= asset_path('shared/tutorial/_tutorial.html') %>'
  controller: 'tutorialCtrl'
  link: (scope, element, attrs)->
    scope.onClose = -> $parse(attrs.onClose)(scope)
    setTimeout (-> element.foundation('reveal', 'open', close_on_background_click: false, close_on_esc: false, animation: 'fade')), 0

DunnoApp.directive 'tutorialStep', ->
  restrict: 'E'
  require: '^tutorial'
  transclude: true
  templateUrl: '<%= asset_path('shared/tutorial/_tutorialStep.html') %>'
  scope: true
  link: (scope, element, attrs, tutorial)->
    scope.id = tutorial.increaseStep()
