searchInput = ($compile) ->
  link = (scope, element, attributes) ->
    moveAttributesToChildInput(scope, element, 'name', 'placeholder', 'class', 'ng-model', 'focus-on')
    searchOnEvent(scope, element)

  moveAttributesToChildInput = (scope, element, attributes...) ->
    input = element.find('input')
    attributes.forEach (attribute) ->
      value = element.attr attribute
      input.attr attribute, value
      element.removeAttr attribute
    $compile(input)(scope)

  searchOnEvent = (scope, element) ->
    element.on 'search', (e) ->
      value = e.target.value
      if value == ""
        scope.clearSearch()
      else
        scope.performSearch()(e.target.value)

  link: link
  restrict: 'E'
  templateUrl: '<%= asset_path("app/components/search/search-input") %>'
  scope:
    performSearch: '&'
    clearSearch: '&'

searchInput.$inject = ['$compile']

angular
  .module('DunnoApp')
  .directive('searchInput', searchInput)
