DunnoApp = angular.module('DunnoApp')

# TODO: Separar controller de show e edit
CourseCtrl = ($scope, $location, $stateParams, $state, $modal, Course, Utils, DateUtils, CourseResolver, course)->
  angular.extend($scope, Utils)
  angular.extend($scope, DateUtils)

  $scope.course = new Course()

  $scope.course.weekly_schedules = [{}]

  $scope.isToday = (event) ->
    if DateUtils.locationInTime(event.start_at) == 'today'
      return true
    else
      return false

  $scope.eventClass = (event) ->
    klass = DateUtils.locationInTime(event.start_at)
    klass

  $scope.fetch = (month) ->
    $scope.$emit('wholePageLoading', Course.get({ uuid: $stateParams.id }, { month: month || $stateParams.month }).then (course)->
      $scope.course = formatToView(course)
    )

  $scope.course = course

  $scope.save = (course) ->
    $scope.isSending = true
    course.save().then(->
      $location.path "/courses/#{course.uuid}/"
    ).finally(-> $scope.isSending = false)

  $scope.delete = (course) ->
    if confirm("Deseja mesmo remover a disciplina #{course.name}? Esta operação não poderá ser desfeita.")
      course.delete().then ->
        $location.path '#/courses'

  formatToView = (course) ->
      course.start_date = $scope.formattedDate(course.start_date, 'DD/MM/YYYY')
      course.end_date   = $scope.formattedDate(course.end_date,   'DD/MM/YYYY')
      course

  $scope.goToEvent = (event) ->
    if $scope.course.user_role == 'teacher'
      $state.go('^.event', { startAt: event.start_at })
    else
      $state.go('^.events', until: event.start_at)

  # TODO: Can we stop using this?
  $scope.canAccessEvent = (event) ->
    event.status == 'published' || $scope.course.user_role == 'teacher'

  $scope.translatedStatus = (event) ->
    if $scope.isCanceled(event)
      'Cancelada'
    else if $scope.isPublished(event)
      'Publicada'
    else if $scope.isDraft(event)
      'Rascunho'

  $scope.isCanceled = (event) ->
    event.formatted_status == 'canceled'

  $scope.isPublished = (event) ->
    event.formatted_status == 'published' ||
    event.formatted_status == 'happened'

  $scope.isDraft = (event) ->
    event.formatted_status == 'draft' ||
    event.formatted_status == 'empty'

  $scope.isTeacher = (course) ->
    course.user_role == 'teacher'

  $scope.isStudent = (course) ->
    course.user_role != 'teacher'

  $scope.openEditCourseForm = ->
    $modal.open
      templateUrl: '<%= asset_path('app/courses/course-edit') %>'
      controller: 'EditCourseCtrl'
      windowClass: 'edit__course full'
      resolve: { course: -> angular.copy($scope.course) }

  $scope.openNotification = ->
    $modal.open
      templateUrl: '<%= asset_path('app/courses/notify/notify-members') %>'
      controller: 'NotificationCtrl'
      windowClass: 'send__notification full'

CourseCtrl.$inject = [
  '$scope', '$location', '$stateParams', '$state', '$modal', 'Course', 'Utils', 'DateUtils', 'CourseResolver', 'course'
]
DunnoApp.controller 'CourseCtrl', CourseCtrl

